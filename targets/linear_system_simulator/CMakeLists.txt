# ==============================================================================
# Target

get_filename_component(SOURCE_DIR_NAME "${CMAKE_CURRENT_SOURCE_DIR}" NAME)
string(REPLACE " " "_" SOURCE_DIR_NAME "${SOURCE_DIR_NAME}")

set(TARGET_NAME "${SOURCE_DIR_NAME}")

list(FIND APP_TARGET_DIR_NAMES ${TARGET_NAME} TARGET_INDEX)
if(${TARGET_INDEX} MATCHES "-1")
    message(FATAL_ERROR "Failed to find the target index!")
endif()
list(GET APP_TARGET_TEXT_NAMES ${TARGET_INDEX} TARGET_TEXT_NAME)

set(TARGET_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set(TARGET_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})
set(TARGET_INSTALL_DIR ${APP_INSTALL_DIR}/targets/${TARGET_NAME})

set(TARGET_INCLUDE_DIRS "${APP_TOOLS_INCLUDE_DIRS}" "${TARGET_BINARY_DIR}")
set(TARGET_LIBRARY_DIRS "${APP_TOOLS_LIBRARY_DIRS}")
set(TARGET_LIBRARIES "${APP_TOOLS_LIBRARIES}")
set(TARGET_COMPILE_OPTIONS "${APP_TOOLS_COMPILE_OPTIONS}")
set(TARGET_LINK_OPTIONS "${APP_TOOLS_LINK_OPTIONS}")

set(TARGET_ROS_NAME "${TARGET_NAME}_ros")
set(TARGET_ROS_INCLUDE_DIRS "${TARGET_INCLUDE_DIRS}" "${TARGET_SOURCE_DIR}/simulator")
set(TARGET_ROS_LIBRARY_DIRS "${TARGET_LIBRARY_DIRS}" "${TARGET_BINARY_DIR}/simulator")
set(TARGET_ROS_LIBRARIES "${TARGET_LIBRARIES}" "${TARGET_NAME}_simulation1")
set(TARGET_ROS_COMPILE_OPTIONS "${TARGET_COMPILE_OPTIONS}")
set(TARGET_ROS_LINK_OPTIONS "${TARGET_LINK_OPTIONS}")
set(TARGET_ROS_MESSAGE_OUTPUT log) # screen / log (used in the launch file)

configure_file(
    "${TARGET_SOURCE_DIR}/README.md"
    "${TARGET_BINARY_DIR}/README.md"
    @ONLY
)
install(
    FILES "${TARGET_BINARY_DIR}/README.md"
    DESTINATION "${TARGET_INSTALL_DIR}"
)
configure_file(
    "${TARGET_SOURCE_DIR}/parameter.xml"
    "${TARGET_BINARY_DIR}/parameter.xml"
)
install(
    FILES "${TARGET_BINARY_DIR}/parameter.xml"
    DESTINATION "${TARGET_INSTALL_DIR}"
)
configure_file(
    "${TARGET_SOURCE_DIR}/target.h"
    "${TARGET_BINARY_DIR}/target.h"
)
configure_file(
    "${TARGET_SOURCE_DIR}/target.py"
    "${TARGET_BINARY_DIR}/${TARGET_NAME}.py"
)
install(
    FILES "${TARGET_BINARY_DIR}/${TARGET_NAME}.py"
    DESTINATION "${TARGET_INSTALL_DIR}"
)

# ==============================================================================
# Simulator

add_subdirectory(simulator)

# ==============================================================================
# ROS

# ------------------------------------------------------------------------------
# create catkin_workspace and ros package

if(NOT EXISTS ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME})
    file(
        WRITE ${TARGET_BINARY_DIR}/ros_package_create.sh
        "#!${APP_SHELL_EXE}\n"
        "source ${APP_CONDA_DIR}/etc/profile.d/conda.sh &&\n"
        "conda activate ${APP_CONDA_ENV_DIR} &&\n"
        "mkdir -p ${TARGET_BINARY_DIR}/ros/src &&\n"
        "cd ${TARGET_BINARY_DIR}/ros &&\n"
        "catkin_make &&\n"
        "source devel/setup.${APP_SHELL_TYPE} &&\n"
        "cd src &&\n"
        "catkin_create_pkg ${TARGET_ROS_NAME} std_msgs rospy roscpp\n"
    )
    execute_process(
        COMMAND chmod +x ros_package_create.sh
        WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
    )
    execute_process(
        COMMAND ./ros_package_create.sh
        WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
    )
    # In macOS, something strange happens with the build directory generated by 
    # catkin_make with execute_process. In order to remove this problem, 
    # the build directory needs to be recreated.
    file(REMOVE_RECURSE "${TARGET_BINARY_DIR}/ros/build")
    file(MAKE_DIRECTORY "${TARGET_BINARY_DIR}/ros/build")
endif()

# ------------------------------------------------------------------------------
# configure the ROS package

file(
    WRITE ${TARGET_BINARY_DIR}/ros_package_configure.sh
    "#!${APP_SHELL_EXE}\n"
    "source ${APP_CONDA_DIR}/etc/profile.d/conda.sh &&\n"
    "conda activate ${APP_CONDA_ENV_DIR} &&\n"
    "cd ${TARGET_BINARY_DIR}/ros/build &&\n"
    "cmake ../src -DCMAKE_INSTALL_PREFIX=${TARGET_INSTALL_DIR}/ros -DCATKIN_DEVEL_PREFIX=../devel"
)
execute_process(
    COMMAND chmod +x ros_package_configure.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
execute_process(
    COMMAND ./ros_package_configure.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)

# ------------------------------------------------------------------------------
# copy files

file(GLOB_RECURSE FILE_LIST CONFIGURE_DEPENDS "ros/*")
foreach(FILE ${FILE_LIST})
    get_filename_component(FILE_DIR ${FILE} DIRECTORY)
    get_filename_component(FILE_NAME ${FILE} NAME)
    get_filename_component(FILE_NAME_WLE ${FILE} NAME_WLE)
    get_filename_component(FILE_LAST_EXT ${FILE} LAST_EXT)
    # message("FILE = ${FILE}")
    # message("     = ${FILE_DIR}/${FILE_NAME_WLE}${FILE_LAST_EXT}")
    if(FILE_NAME MATCHES "CMakeLists.txt" OR FILE_NAME MATCHES "package.xml")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/${FILE_NAME}
            @ONLY
        )
    elseif(FILE_NAME MATCHES ".msg$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/msg/${FILE_NAME}
        )
    elseif(FILE_NAME MATCHES ".srv$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/srv/${FILE_NAME}
        )
    elseif(FILE_NAME MATCHES ".action$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/action/${FILE_NAME}
        )
    elseif(FILE_NAME MATCHES ".cpp$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/src/${FILE_NAME}
        )
    elseif(FILE_NAME MATCHES ".py$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/scripts/${FILE_NAME}
        )
        execute_process(
            COMMAND chmod +x ${FILE_NAME}
            WORKING_DIRECTORY ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/scripts
        )
    elseif(FILE_NAME MATCHES ".launch$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/launch/${FILE_NAME}
        )
        execute_process(
            COMMAND chmod +x ${FILE_NAME}
            WORKING_DIRECTORY ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/launch
        )
    elseif(FILE_NAME MATCHES ".sh$")
        configure_file(
            ${FILE}
            ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/test/${FILE_NAME_WLE}
        )
        execute_process(
            COMMAND chmod +x ${FILE_NAME_WLE}
            WORKING_DIRECTORY ${TARGET_BINARY_DIR}/ros/src/${TARGET_ROS_NAME}/test
        )
    else()
        message("Unregistered file type: ${FILE_NAME}")
    endif()
endforeach()

# ------------------------------------------------------------------------------
# clean and configure the ROS package

file(
    WRITE ${TARGET_BINARY_DIR}/ros_package_clean.sh
    "#!${APP_SHELL_EXE}\n"
    "source ${APP_CONDA_DIR}/etc/profile.d/conda.sh &&\n"
    "conda activate ${APP_CONDA_ENV_DIR} &&\n"
    "cd ${TARGET_BINARY_DIR}/ros/build &&\n"
    "cmake --build . --target clean"
)
execute_process(
    COMMAND chmod +x ros_package_clean.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
add_custom_target(${TARGET_NAME}_ros_clean ALL
    COMMAND ./ros_package_clean.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
add_custom_target(${TARGET_NAME}_ros_config ALL
    COMMAND ./ros_package_configure.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
add_dependencies(
    ${TARGET_NAME}_ros_config
    ${TARGET_NAME}_ros_clean
)

# ------------------------------------------------------------------------------
# build the ROS package

file(
    WRITE ${TARGET_BINARY_DIR}/ros_package_build.sh
    "#!${APP_SHELL_EXE}\n"
    "source ${APP_CONDA_DIR}/etc/profile.d/conda.sh &&\n"
    "conda activate ${APP_CONDA_ENV_DIR} &&\n"
    "cd ${TARGET_BINARY_DIR}/ros/build &&\n"
    "make"
)
execute_process(
    COMMAND chmod +x ros_package_build.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
add_custom_target(${TARGET_NAME}_ros_build ALL
    COMMAND ./ros_package_build.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
add_dependencies(
    ${TARGET_NAME}_ros_build
    ${TARGET_NAME}_ros_config
)

# ------------------------------------------------------------------------------
# install the ROS package

file(
    WRITE ${TARGET_BINARY_DIR}/ros_package_install.sh
    "#!${APP_SHELL_EXE}\n"
    "source ${APP_CONDA_DIR}/etc/profile.d/conda.sh &&\n"
    "conda activate ${APP_CONDA_ENV_DIR} &&\n"
    "cd ${TARGET_BINARY_DIR}/ros/build &&\n"
    "make install"
)
execute_process(
    COMMAND chmod +x ros_package_install.sh
    WORKING_DIRECTORY "${TARGET_BINARY_DIR}"
)
install(
    CODE "execute_process(COMMAND ./ros_package_install.sh WORKING_DIRECTORY ${TARGET_BINARY_DIR})"
)